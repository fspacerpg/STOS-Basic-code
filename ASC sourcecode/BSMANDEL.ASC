10 key off : click off : curs off : hide : get palette (10) : dim A$(1000)
100 rem game start
105 goto 2100
106 rem start game
107 cls : get palette (10)
110 X1=50 : Y1=170 : X2=220 : Y2=170 : YAIR1=-1 : YAIR2=-1 : XAIR1=50 : XAIR2=220 : x mouse=220 : MX=220 : F1=0 : F2=0
150 sprite 3,XAIR1,180,15 : sprite 4,XAIR2,180,15
160 HIT1=10 : HIT2=10
170 pen 8 : locate 0,0 : print "PLAYER 1:" : locate 18,0 : pen 10 : print "PLAYER 2:" : pen 15 : locate 10,0 : print "10" : locate 28,0 : print "10"
200 rem movement stage
210 sprite 1,X1,Y1,1 : sprite 2,X2,Y2,2
300 rem check joysticks
305 if F2=0 and fire then sprite 10,X2,Y2,3 : move x 10,"(1,-2,1)l" : move on : F2=1 : shoot 
306 if F1=0 and inkey$=" " then sprite 11,X1,Y1,3 : move x 11,"(1,2,1)l" : move on : F1=1 : shoot 
307 if F1=1 and x sprite(11)>319 then F1=0
308 if F2=1 and x sprite(10)<2 then F2=0
310 if jright then X2=X2+1
320 if jleft then X2=X2-1
330 if x mouse<MX then M1=-1
340 if x mouse>MX then M1=1
350 MX=x mouse
351 X1=X1+M1
352 if collide(1,4,4)=1024 then HIT1=HIT1-1 : locate 10,0 : print HIT1;" "
353 if collide(2,4,4)=2048 then HIT2=HIT2-1 : locate 28,0 : print HIT2;" "
355 if X2>319 then X2=2
356 if X2<1 then X2=318
357 if X1<1 then X1=318
358 if X1>319 then X1=2
360 wait vbl 
400 goto 1000
1000 rem air movement
1009 if X2<XAIR1+20 and X2>XAIR1 then YAIR2=-1 : goto 1035
1010 if X2<XAIR2+20 and X2>XAIR2 then YAIR2=-1 : goto 1035
1030 YAIR2=1
1035 if Y2<10 then Y2=169
1036 if Y2>170 then Y2=11
1040 Y2=Y2+YAIR2
1059 if X1<XAIR1+20 and X1>XAIR1 then YAIR1=-1 : goto 1066
1060 if X1<XAIR2+20 and X1>XAIR2 then YAIR1=-1 : goto 1066
1065 YAIR1=1
1066 if Y1>170 then Y1=11
1067 if Y1<10 then Y1=169
1080 Y1=Y1+YAIR1
1100 if HIT1=0 then SP=1 goto 2000
1110 if HIT2=0 then SP=2 goto 2000
1150 goto 200
2000 rem END OF GAME
2010 anim SP,"(4,6)(5,6)(6,6)(7,6)(8,6)(9,6)(10,6)(11,6)(12,6)(13,6)(14,6)" : music 5 : anim on 
2030 windopen 1,10,10,20,5,1,1
2050 print " Game over, Glider     ";SP;" loses!"
2060 if fire or inkey$=" " then windel 1 : sprite off : goto 2100
2070 goto 2060
2100 rem title page
2105 screen copy 10 to physic
2110 music 1
2200 restore : read A$
2210 for T=1 to len(A$)-20
2215 if inkey$=" " or fire then music off : goto 106
2220 B$=mid$(A$,T,30) : locate 5,20 : print B$ : wait 8 : next T
3000 goto 2200
4000 data "        Welcome to SKY Software and Phenoix combined effort of GLIDE TIME.  This product is PD, please copy.  Instructions....simply shoot the opposition ten times to win the contest. This is a two player game only. Play one uses the mouse to move and the 'Space' bar to shoot.Player two uses the joystick. Use the air vents at the bottom of the screen to lift your glider to the top of the play feild. Thats it! Manderin software, STOS is great, but whens the STE version comming (STOS Plus?)."
